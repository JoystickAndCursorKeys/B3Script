10 SYNCH =20 : VOL = 0.5: DISPLAY 100
15 CLS RESET :IMMEDIATE  1:VOLUME VOL : chvolume 0,1 : chvolume 1,.5
20 SW =COLS ():SH =ROWS ():TAW =80:TAH =35:SBH = 4 : gameover = 0
30 DIM LIV$ ( 3) : DIM WALLH(3)
40 DIM SCL ( 2):SCL ( 0)= 5:SCL ( 1)=11
45 DIM LVLC(10): DIM LVLWC(10) : DIM LVLWCC(10)
50 COLOR  1, 0, 0:CLS
60 TAX0 =INT ((SW / 2)-(TAW / 2)):TAY0 =INT ((SH / 2)-(TAH / 2))
70 IF TAX0 < 0OR TAY0 < 0THEN COLOR  2, 0, 2:CLS RESET :PRINT "SCREEN TO SMALL!"
80 TEXTAREA TAW ,TAH ,TAX0 ,TAY0
91 WALLH(0) = 532:WALLH(1) = 532:WALLH(2) = 532
92 read colcount
95 FOR I = 0 TO colcount-1: READ D : LVLC(I) = D : READ D : LVLWC(I) = D :
100 READ D: LVLWCC( I ) = D: NEXT
200 CXWIDTH = width() / cols(): CXHEIGHT = height() / rows():
210 def fn XPOS(x) = CXWIDTH * x
220 def fn YPOS(y) = CXHEIGHT * y

1000 SUB MAINLOOP
1010 GOSUB TITLE
1020 gameover = 0 : GOSUB GAME
1030 GOTO MAINLOOP

2000 SUB TITLE
2010 TEXTAREA TAW ,TAH ,TAX0 ,TAY0
2020 COLOR  5,10:CLS
2030 LOCATE INT (ROWS ()/ 2)-10, 0
2031 CENTER "Snake B3":PRINT : COLOR  1,10
2035 if gameover = 1 then CENTER "Game Over":PRINT
2036 if gameover = 1 then CENTER "Your Score: " ; int( SC ) :PRINT
2050 CENTER "Press spacebar to start"
2060 PRINT
2070 CENTER "Use the following controls" : print : COLOR  2,0
2080 CENTER "W"
2090 CENTER "ASD"
2100 REM GOSUB WAITKEY
2101 GET A$
2102 IF A$ ="1"THEN  six = 10: gosub sound
2103 IF A$ ="2"THEN  six = 11: gosub sound
2104 IF A$ <> " " then goto 2101
2110 CLS
2120 RETURN

3000 SUB GAME
3010 WALL =528:HRTH =9829:CLOVER =9827: STARCH0 = 608: STARCH1 =  609:  GOALCH0=9654 : GOALCHA0=9664 : goalch = 296
3015 SNAKE = 9608
3020 GOSUB DRAWLEVEL
3030 TEXTAREA TAW ,SBH ,TAX0 ,TAY0 :COLOR  1, 0:CLS
3040 SC = 0:FLIP0=0:FLIP = 0:DC = 0 : Level = 0
3060 LIVES = 3
3070 LIV$ ( 3)=UC$ (HRTH );UC$ (HRTH );UC$ (HRTH )
3080 LIV$ ( 2)=UC$ (HRTH );UC$ (HRTH );" "
3090 LIV$ ( 1)=UC$ (HRTH );"  "
3100 REM RESUME NEXT LEVEL OR NEXT LIVE
3101 GOSUB LIVESTATUS
3102 X =INT (TAW / 2):Y =INT (TAH / 2):XD = 0:YD = 1
3103 XI =INT (X ):YI =INT (Y ):LXI =XI :LYI =YI : YF=.8: XF=1 :SPF = .1: SPC = 0: SPCI = .01
3104 XSP =  1 * SPF :YSP =YF * SPF
3105 CD = 10: CDS = .01
3106 GOSUB DRAWTIMER
3107 IF gameover = 1 then return
3110 REM GAMELOOP
3115 SPC = SPC + SPCI: IF SPC > 1 THEN SPC = 0 : GOSUB GOFASTER
3130 POKECCL TAX0 +XI ,TAY0 +YI ,SNAKE ,SCL (FLIP ),0
3150 GETKEY KEY$
3160 if key$ <> "" then gosub HANDLEKEYS
3200 X =X +(XD *XSP )
3210 Y =Y +(YD *YSP )
3220 LXI =XI :LYI =YI :XI =INT (X ):YI =INT (Y )
3230 IF LXI <>XI OR LYI <>YI THEN FLIP0 = 1-FLIP0: IF FLIP0 = 0 then FLIP = 1 - FLIP
3240 GOSUB TESTPLAYER
3250 IF DIE = 1THEN GOSUB PLAYERDIE : GOTO 3100
3280 if nextlev = 1 then POKECCL TAX0 +XI ,TAY0 +YI ,SNAKE ,SCL (FLIP ),0
3282 if nextlev = 1 then xi=xi + 1 : flip = 1-flip : POKECCL TAX0 +XI ,TAY0 +YI ,SNAKE ,SCL (FLIP ),0
3283 if nextlev = 1 then gosub NEXTLEVEL : Level = Level + 1 : SC=SC+100: GOSUB REDRAWINGAME : goto 3100
3295 X =RANGE (X , 0,TAW - 1):Y =RANGE (Y , 0,TAH - 1)
3300 GOSUB WAIT
3310 SC =SC + .1: COLOR  1:LOCATE  0, 1:PRINT "Score: "; pad$( str$( int( sc ), 10, 0), "0", 6 )
3315 CD=CD-CDS
3316 GOSUB DRAWTIMER
3320 GOTO 3110
3330 END

3400 SUB HANDLEKEYS
3410 IF KEY$ ="w" OR KEY$ = "ArrowUp" THEN YD =- 1:XD = 0 : six = 0: gosub sound
3420 IF KEY$ ="s" OR KEY$ = "ArrowDown" THEN YD = 1:XD = 0  : six = 0: gosub sound
3430 IF KEY$ ="a" OR KEY$ = "ArrowLeft" THEN XD =- 1:YD = 0 : six = 0: gosub sound
3440 IF KEY$ ="d" OR KEY$ = "ArrowRight" THEN XD = 1:YD = 0  : six = 0: gosub sound
3499 RETURN

3500 SUB GOFASTER
3510 SPF = SPF + .1
3511 SPF = RANGE( SPF, 0, 1)
3515 XSP =  1 * SPF :YSP =YF * SPF
3516 PSPF = INT(SPF*10)
3525 IF PSPF = LSPF THEN RETURN
3540 LSPF = PSPF
3545 SIX=1 : GOSUB SOUND
3550 RETURN

3600 SUB UPDATELEVELINDICATOR
3620 LVL$ = "   Level: " + Level
3630 TEXTAREA TAW ,TAH ,TAX0 ,TAY0
3640 LOCATE  0,0: COLOR  5, 0 : CENTER LVL$
3650 RETURN

3700 SUB DRAWTIMER
3710 LIW = INT((TAW-2) * (CD/10)) : LIW2 = TAW-LIW
3720 FOR T=1 TO TAW-2
3730 TCH = 9675 : TCC = 14
3740 if LIW >T then TCH = 9679
3745 IF T<15 then tcc = 5
3746 IF T<10 then tcc = 11
3747 IF T<5 then tcc = 2
3750 pokeccl t+TAX0,2+TAY0,tch, TCC,0
3760 next
3770 RETURN

4000 SUB DRAWLEVEL
4001 TEXTAREA TAW ,TAH ,TAX0 ,TAY0  : HH = TAH-SBH: WW = TAW
4005 LVI = INT(Level / 5) % 4
4010 COLOR  LVLWC(LVI), LVLC(LVI):CLS
4015 for t = 0 to Level
4016 sxx = INT(rnd(0)*(WW-1))+TAX0: syy= INT(rnd(0)*HH)+TAY0 + SBH
4017 pokeccl sxx, syy, STARCH0, 5
4018 pokeccl sxx+1, syy, STARCH1, 5
4019 next
4020 SPW =WIDTH ():SPH =HEIGHT ()
4060 FOR TX = 0TO TAW - 1
4065  CH = WALLH( TX % 3)
4070  POKECCL TAX0 +TX ,TAY0 +SBH ,CH,LVLWC(LVI),LVLWCC(LVI) :POKECCL TAX0 +TX ,TAY0 +TAH - 1, CH,LVLWC(LVI),LVLWCC(LVI)
4080 NEXT
4090 FOR TY =SBH TO TAH - 1
4095  CH = WALLH( TY % 3)
4100  POKECCL TAX0 ,TAY0 +TY ,CH,LVLWC(LVI),LVLWCC(LVI) :POKECCL TAX0 +TAW - 1,TAY0 +TY ,CH,LVLWC(LVI),LVLWCC(LVI)
4110 NEXT
4120 LHEIGHT = TAH-2-7 : Y = INT(RND(0)*LHEIGHT) + TAY0 + 3
4125 X=TAX0 +TAW - 2 : X2 = X+1 : GC0 = GOALCH0: GC1 = GOALCH
4126 if Level % 2 = 0 then x = TAX0: x2 = x+1 : GC0 = GOALCH: GC1 = GOALCHA0
4130 POKECCL X2, Y, WALL, LVLWC(LVI), LVLWCC(LVI): POKECCL X, Y, WALL, LVLWC(LVI),LVLWCC(LVI)
4135 POKECCL X2, Y+5, WALL, LVLWC(LVI), LVLWCC(LVI) : POKECCL X, Y+5, WALL, LVLWC(LVI),LVLWCC(LVI)
4140 Y=Y+1
4150 FOR TY=0 TO 3
4155  POKECCL X2,Y+TY,GC0,14,LVLC(LVI): POKECCL X,Y+TY,GC1,14,LVLC(LVI)
4160 NEXT
4200 Y=Y-2
4205 X = TAW - 10
4210 FOR YY=0 to 6 : POKECCL X, YY + Y, WALL, LVLWC(LVI),LVLWCC(LVI) : NEXT
4300 WL = 7: LHEIGHT = TAH-2-(2*WL) : LWIDTH = TAW-2-(2*WL)
4305 FOR T = 0 TO LEVEL
4310  Y = INT(RND(0)*LHEIGHT) + TAY0 + WL
4315  X = INT(RND(0)*LWIDTH) + TAX0 + WL
4320  IF T % 2 = 1 THEN FOR YY=0 to WL-1 : POKECCL X,    YY + Y, WALL, LVLWCC(LVI), LVLWC(LVI) : NEXT
4325  IF T % 2 = 0 THEN FOR XX=0 to WL-1 : POKECCL XX+X, Y,      WALL, LVLWCC(LVI), LVLWC(LVI) : NEXT
4330 NEXT
4500 RETURN

5000 SUB TESTPLAYER
5010 DIE = 0 : NEXTLEV = 0
5015 IF CD <= 0 then DIE = 1: RETURN
5020 IF (LXI =XI AND LYI =YI )THEN RETURN
5025 PCHAR = PEEKC (XI +TAX0 ,YI +TAY0 )
5026 IF PCHAR = 32 THEN  RETURN
5030 IF PCHAR = GOALCH THEN NEXTLEV = 1 : RETURN
5035 IF PCHAR = STARCH0 THEN SC=SC+200 : SIX=20 : GOSUB SOUND : POKEC XI +TAX0 + 1 ,YI +TAY0, 32: RETURN
5036 IF PCHAR = STARCH1 THEN SC=SC+200 : SIX=20 : GOSUB SOUND : POKEC XI +TAX0 - 1 ,YI +TAY0, 32: RETURN
5040 IF PCHAR<>32 THEN DIE = 1 : RETURN
5090 return

5100 SUB REDRAWINGAME
5110 TEXTAREA TAW ,TAH ,TAX0 ,TAY0 : gosub DRAWLEVEL
5120 TEXTAREA TAW ,SBH ,TAX0 ,TAY0 :COLOR  1, 0:CLS : gosub LIVESTATUS
5130 GOSUB UPDATELEVELINDICATOR
5140 RETURN

5500 SUB NEXTLEVEL
5510 TEXTAREA TAW ,TAH ,TAX0 ,TAY0
5515 COLOR  1, 3
5520 LOCATE INT (ROWS ()/ 2)- 5, 0:CENTER "  Enter Level " + ( Level + 1)
5525 CENTER "Your Score: ";int( SC )
5530 SIX=12 : GOSUB SOUND
5535 GOSUB WAITKEY
5540 RETURN

6000 SUB PLAYERDIE
6005 LIVES =LIVES - 1: IF LIVES = 0THEN gameover = 1
6010 TEXTAREA TAW ,TAH ,TAX0 ,TAY0
6020 COLOR  1, 2
6030 LOCATE INT (ROWS ()/ 2)- 5, 0:CENTER "  1 Live Lost  "
6040 CENTER "Your Score: ";int( SC )
6050 SIX=10 : GOSUB SOUND
6060 GOSUB WAITKEY
6065 GOSUB REDRAWINGAME
6070 RETURN

7000 SUB WAITKEY
7010 GET K$
7020 IF K$ <>" "THEN GOTO 7010
7030 RETURN

8000 SUB LIVESTATUS
8010 LOCATE  0,TAW - 4:COLOR  2, 0:PRINT LIV$ (LIVES );
8020 RETURN

9500 SUB WAIT
9510 WTM =TI +SYNCH
9520 IF TI <WTM THEN GOTO 9520
9530 RETURN

10000 sub sound
10010 if six = 0 then chvolume 0,.5: BEEP  1,50,70 : return
10020 if six = 1 then chvolume 0,.5: BEEP  0,930,70 : return
10100 if six = 10 then gosub sndweepweepweep : return
10200 if six = 11 then gosub sndweepweepweep2 : return
10300 IF SIX = 12 THEN GOSUB sndweepweep : return
10500 IF SIX = 20 THEN GOSUB sndweepweep2 : return
10900 print "unknown sound"

11000 sub sndweepweepweep
11010 CH=2: FC=0
11020 SETADR CH ,20,20,20
11030 CHVOLUME CH , 1
11040 CHSVOLUME CH ,.5
11050 SOUND CH ,400,100000
11060 GOSUB WAIT
11070 CHFREQ CH ,F
11080 FC=FC+.1: F=(saw(fc)*1800)
11090 IF FC<5 then goto 11060
11095 beep ch, 1,1 : RETURN

12000 sub sndweepweepweep2
12010 CH=2: FC=0
12020 SETADR CH ,20,20,20
12030 CHVOLUME CH , 1
12040 CHSVOLUME CH ,.5
12050 SOUND CH ,400,100000
12060 GOSUB WAIT
12070 CHFREQ CH ,F : VOLUME ((FC)/525) * VOL
12080 FC=FC+5: F=(sin(fc)*900)+900
12090 IF FC<525 then goto 12060
12095 beep ch, 1,1 : volume vol
12099 RETURN

13000 sub sndweepweep
13010 CH=2: FC=0 : MSN = 45: MSN2=.02
13020 SETADR CH ,20,20,20
13030 CHVOLUME CH , 1
13040 CHSVOLUME CH ,.5
13050 SOUND CH ,400,100000
13060 GOSUB WAIT
13070 CHFREQ CH ,F : REM VOLUME ((FC)/MSN) * VOL
13080 FC=FC+MSN2: F=1100-(TRI(fc)*1100)  : IF( TRI(FC)<.1 ) THEN MSN2=MSN2+.01
13090 IF FC<=1 then goto 13060
13095 beep ch, 1,1 : volume vol
13099 RETURN

14000 sub sndweepweep2
14010 CH=1: FC=0 : MSN = 45: MSN2=.04
14020 SETADR CH ,20,20,20
14030 CHVOLUME CH , 1
14040 CHSVOLUME CH ,.5
14050 SOUND CH ,400,100000
14060 GOSUB WAIT
14070 CHFREQ CH ,F
14080 FC=FC+MSN2: F=1100-(TRI(fc)*1100)  : IF( TRI(FC)<.1 ) THEN MSN2=MSN2+.01
14090 IF FC<=1 then goto 14060
14095 beep ch, 1,1 : volume vol
14099 RETURN

50000 sub data
50049 data 7
50050 data 20,6,26,  23,7,8,   0,1,8,   4,7,8,   10,11,8,   0,4,8  ,   12,13,8
